apply plugin: 'com.android.application'
apply plugin: 'org.jetbrains.kotlin.android'
apply plugin: 'org.jetbrains.kotlin.android.extensions'
apply plugin: 'org.jetbrains.kotlin.kapt'
apply plugin: 'androidx.navigation.safeargs'

android {
    compileSdkVersion 29

    defaultConfig {
        applicationId "io.zluan.logoswap"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    compileOptions {
        sourceCompatibility rootProject.ext.java_version
        targetCompatibility rootProject.ext.java_version
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    // Set the source of tests to same for both Unit and Instrumented tests
    sourceSets {
        String sharedTestDir = 'src/test/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }
    splits {
        abi {
            enable true
            reset()
            if (rootProject.ext.ci || gradle.startParameter.taskNames.any { it.contains('release') }) {
                include 'armeabi', 'armeabi-v7a', 'armeabi-v8a', 'mips', 'mips64', 'x86', 'x86_64'
            } else {
                include 'x86_64'
            }
        }
    }
    // Necessary for Robolectric unit tests
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.1.0'
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.0'

    // App compat
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.2.0-alpha05'

    // Navigation library
    def nav_version = '2.1.0'
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"

    // CameraX
    def camerax_version = '1.0.0-alpha05'
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.9.0'
    kapt 'com.github.bumptech.glide:compiler:4.9.0'

    // UI
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    // PixelMod
    implementation project(':pixelmod')

    // Unit testing
    testImplementation 'androidx.test.ext:junit:1.1.1'
    testImplementation 'androidx.test:rules:1.2.0'
    testImplementation 'androidx.test:runner:1.2.0'
    testImplementation 'androidx.test.espresso:espresso-core:3.2.0'
    testImplementation 'org.robolectric:robolectric:4.3'

    // Instrumented testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
